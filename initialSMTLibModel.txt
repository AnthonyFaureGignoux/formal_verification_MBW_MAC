; ---------------
; BASIC FUNCTIONS 
; ---------------
; Define the bit extension functions
(define-fun ext8ToU32 ((x (_ BitVec 8) )) (_ BitVec 32)
    ((_ zero_extend 24) x)
)

(define-fun ext8ToU16 ((x (_ BitVec 8) )) (_ BitVec 16)
    ((_ zero_extend 8) x)
)

(define-fun ext16ToU32 ((x (_ BitVec 16) )) (_ BitVec 32)
    ((_ zero_extend 16) x)
)

(define-fun ext8ToS32 ((x (_ BitVec 8) )) (_ BitVec 32)
    ((_ sign_extend 24) x)
)

(define-fun ext8ToS16 ((x (_ BitVec 8) )) (_ BitVec 16)
    ((_ sign_extend 8) x)
)

(define-fun ext16ToS32 ((x (_ BitVec 16) )) (_ BitVec 32)
    ((_ sign_extend 16) x)
)

; Define the multiplication functions
(define-fun mul32 
    (
        (x (_ BitVec 8))
        (y (_ BitVec 8))
        (xSigned Bool)
        (ySigned Bool)
    )
    (_ BitVec 32)
    (bvmul
        (ite xSigned (ext8ToS32 x) (ext8ToU32 x))
        (ite ySigned (ext8ToS32 y) (ext8ToU32 y))
    )
)

; Define the multiplier (reference)
(define-fun mul16
    (
        (x (_ BitVec 8))
        (y (_ BitVec 8))
        (xSigned Bool)
        (ySigned Bool)
    )
    (_ BitVec 16)
    (bvmul
        (ite xSigned (ext8ToS16 x) (ext8ToU16 x))
        (ite ySigned (ext8ToS16 y) (ext8ToU16 y))
    )
)

; ---------------------------
; REFERENCE COMPUTATION modes
; ---------------------------
; Define the Full-Precision computation mode (reference)
(define-fun FP 
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
    )
    (_ BitVec 32)
    (bvadd 
        (bvshl (mul32 a_h b_h true true) #x00000010)
        (bvshl 
            (bvadd (mul32 a_h b_l true false) (mul32 a_l b_h false true))
            #x00000008 )
        (mul32 a_l b_l false false)
    )
)

; Define the Half-Precision computation without SP (reference)
(define-fun HP_NSP 
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
    )
    (_ BitVec 32)
    (bvadd 
        (bvshl (mul32 a_h b_h true true) #x00000010)
        (ext16ToU32 (mul16 a_l b_l true true))
    )
)

; Define the Half-Precision computation with SP (reference)
(define-fun HP_SP 
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
    )
    (_ BitVec 32)
    (bvadd 
        (bvshl (mul32 b_h a_l true true) #x00000010)
        (ext16ToU32 (mul16 a_h b_l true true))
    )
)

; ------------------------
; NOMINAL STAGES MODELLING
; ------------------------
; Model the MULTIPLIER in: (a, b, e, aSigned, bSigned) => out: M (16-bit vector)
(define-fun M
    (
        (a (_ BitVec 8))
        (b (_ BitVec 8))
        (e Bool)
        (aSigned Bool)
        (bSigned Bool)
    )
    (_ BitVec 16)
    (ite e (mul16 a b aSigned bSigned) #x0000)
)

; Model the PERMUTATION STAGE in: (a_h, a_l, SP) => out: a_i (8-bit vector)
(define-fun a_1 
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (SP Bool)
    )
    (_ BitVec 8)
    (ite SP a_l a_h)
)

(define-fun a_0 
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (SP Bool)
    )
    (_ BitVec 8)
    (ite SP a_h a_l)
)

(define-fun b_1 
    (
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
        (SP Bool)
    )
    (_ BitVec 8)
    b_h
)

(define-fun b_0 
    (
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
        (SP Bool)
    )
    (_ BitVec 8)
    b_l
)

; Model the PARTIAL PRODUCTS - INTERMEDIATE RESULTS connection (hardwired) 
;    in: (M_ij) => out: M_x (32-bit vector)
(define-fun M_l 
    (
        (M_00_r (_ BitVec 16))
    )
    (_ BitVec 32)
    (ext16ToU32 M_00_r)
)

(define-fun M_h 
    (
        (M_11_r (_ BitVec 16))
    )
    (_ BitVec 32)
    (ext16ToS32 M_11_r)
)

(define-fun M_m
    (
        (M_01_r (_ BitVec 16))
        (M_10_r (_ BitVec 16))
    )
    (_ BitVec 32)
    (bvadd (ext16ToS32 M_01_r) (ext16ToS32 M_10_r))
)

; Model the FINAL RESULT in: (M_h, M_m, M_l) => out: M_r (32-bit vector)
(define-fun M_r 
    (
        (M_h_r (_ BitVec 32))
        (M_m_r (_ BitVec 32))
        (M_l_r (_ BitVec 32))
    )
    (_ BitVec 32)
    (bvadd 
        (bvshl M_h_r #x00000010) ; 2^16
        (bvshl M_m_r #x00000008) ; 2^8
        M_l_r                    ; 2^0
    )
)


; -------------------------------------------------------
; THE MBW MAC MODEL without accumulation and shift stages
; -------------------------------------------------------
; Model the full MBWMAC
;    in: (ah, al, bh, bl, e_00, e_01, e_10, e_11, a1Signed, a0Signed, b1Signed, b0Signed, c_acc, SP) => out: MBWMAC (32-bit vector)
(define-fun MBWMAC
    (
        (a_h (_ BitVec 8))
        (a_l (_ BitVec 8))
        (b_h (_ BitVec 8))
        (b_l (_ BitVec 8))
        (e_00 Bool)
        (e_01 Bool)
        (e_10 Bool)
        (e_11 Bool)
        (a1Signed Bool)
        (a0Signed Bool)
        (b1Signed Bool)
        (b0Signed Bool)
        (c_acc Bool)
        (SP Bool)
    )
    (_ BitVec 32)
    (let 
        (
            (M_00 
                (M
                    (a_0 a_h a_l SP)
                    (b_0 b_h b_l SP)
                    e_00
                    a0Signed
                    b0Signed
                )
            )
            (M_01 
                (M
                    (a_0 a_h a_l SP)
                    (b_1 b_h b_l SP)
                    e_01
                    a0Signed
                    b1Signed
                )
            )
            (M_10 
                (M
                    (a_1 a_h a_l SP)
                    (b_0 b_h b_l SP)
                    e_10
                    a1Signed
                    b0Signed
                )
            )
            (M_11 
                (M
                    (a_1 a_h a_l SP)
                    (b_1 b_h b_l SP)
                    e_11
                    a1Signed
                    b1Signed
                )
            )
        )
        (M_r
            (M_h M_11)
            (M_m M_01 M_10)
            (M_l M_00)
        )
    )
)

; ----------------------------
; DECLARATION OF THE CONSTANTS
; ----------------------------
; Declare any bitvector 
(declare-const a_h (_ BitVec 8))
(declare-const a_l (_ BitVec 8))
(declare-const b_h (_ BitVec 8))
(declare-const b_l (_ BitVec 8))

; Declare any configuration boolean in Conf
(declare-const e_00 Bool)
(declare-const e_01 Bool)
(declare-const e_10 Bool)
(declare-const e_11 Bool)
(declare-const a1Signed Bool)
(declare-const a0Signed Bool)
(declare-const b1Signed Bool)
(declare-const b0Signed Bool)
(declare-const c_acc Bool)
(declare-const SP Bool)


; ----------------------------
; INSTANCIATION OF THE CIRCUIT
; ----------------------------
; The operations ah, al, bh, bl, e_00, e_01, e_10, e_11, a1Signed, a0Signed, b1Signed, b0Signed, c_acc, SP
(define-const result (_ BitVec 32) 
    (MBWMAC a_h a_l b_h b_l e_00 e_01 e_10 e_11 a1Signed a0Signed b1Signed b0Signed c_acc SP)
)

; Util constant
; ----------------------
; THE CONFIGURATION CONF
; ----------------------
(define-const Conf_FP Bool
    (and e_00 e_01 e_10 e_11 a1Signed (not a0Signed) b1Signed (not b0Signed) (not c_acc) (not SP))
)

(define-const Conf_HP_NSP Bool
    (and e_00 (not e_01) (not e_10) e_11 a1Signed a0Signed b1Signed b0Signed c_acc (not SP))
)

(define-const Conf_HP_SP Bool
    (and e_00 (not e_01) (not e_10) e_11 a1Signed a0Signed b1Signed b0Signed c_acc SP)
)


; ----------------------
; START THE TESTING AREA
; ----------------------
; -------------------------------------
; Testing original MBWMAC in error-free 
; -------------------------------------
(echo "[INFO] Analysis in the nominal case for signed integer")

; Testing configuration for FP
; ----------------------------
(push) ; Push 0
(assert Conf_FP)

; Testing a particular example (design is functional)
(push) ; Push 1
(assert (= a_h #x33))
(assert (= a_l #xCC))
(assert (= b_h #x42))
(assert (= b_l #x18))

(assert (= (a_1 a_h a_l SP) #x33))
(assert (= (a_0 a_h a_l SP) #xCC))
(assert (= (b_1 b_h b_l SP) #x42))
(assert (= (b_0 b_h b_l SP) #x18))

(assert (= (M_l #x1320) #x00001320))
(assert (= (M_h #x0D26) #x00000D26))
(assert (= (M_m #x3498 #x04C8) #x00003960))

(assert (= (M_r #x00000D26 #x00003960 #x00001320) #x0D5F7320))

(assert (= result #x0D5F7320))

(assert (= result (FP a_h a_l b_h b_l)))

(echo "[INFO] Trying FP on a valid example, should be SAT and it is: ")
(check-sat) 
(pop) ; Reset up to Push 1

; Verify there is no inconsistent case (shall be unsat)
(push) ; Push 2
(assert (not (= result (FP a_h a_l b_h b_l))))
(echo "[INFO] Proving that the configuration is equivalent to FP, should be UNSAT and it is: ")
(check-sat)
(pop) ; Reset up to Push 2 (remove soudness proof assertion)

; Empty the assert queue
(pop) ; Reset up to Push 0 (going out of FP mode)

; Testing configuration for HP_NSP
; --------------------------------
(push) ; Push 3
(assert Conf_HP_NSP)

; Testing a particular example (design is functional)
(push) ; Push 4
(assert (= a_h #x33))
(assert (= a_l #xCC))
(assert (= b_h #x42))
(assert (= b_l #x18))

(assert (= (a_1 a_h a_l SP) #x33))
(assert (= (a_0 a_h a_l SP) #xCC))
(assert (= (b_1 b_h b_l SP) #x42))
(assert (= (b_0 b_h b_l SP) #x18))

(assert (= (M_l #xFB20) #x0000FB20))
(assert (= (M_h #x0D26) #x00000D26))
(assert (= (M_m #x0000 #x0000) #x00000000))

(assert (= (M_r #x00000D26 #x00000000 #x0000FB20) #x0D26FB20))

(assert (= result #x0D26FB20))

(assert (= result (HP_NSP a_h a_l b_h b_l)))

(echo "[INFO] Trying HP_NSP on a valid example, should be SAT and it is: ")
(check-sat)
(pop) ; Reset up to Push 4

; Verify there is no inconsistent case (shall be unsat)
(push) ; Push 5
(assert (not (= result (HP_NSP a_h a_l b_h b_l))))
(echo "[INFO] Proving that the configuration is equivalent to HP_NSP, should be UNSAT and it is: ")
(check-sat)
(pop) ; Reset up to Push 5

; Empty the assert queue
(pop) ; Reset up to Push 3

; Testing configuration for HP_SP
; -------------------------------
(push) ; Push 6
(assert Conf_HP_SP)

; Testing a particular example (design is functional)
(push) ; Push 7
(assert (= a_h #x33))
(assert (= a_l #xCC))
(assert (= b_h #x42))
(assert (= b_l #x18))

(assert (= (a_1 a_h a_l SP) #xCC))
(assert (= (a_0 a_h a_l SP) #x33))
(assert (= (b_1 b_h b_l SP) #x42))
(assert (= (b_0 b_h b_l SP) #x18))

(assert (= (M_l #x04C8) #x000004C8))
(assert (= (M_h #xF298) #xFFFFF298))
(assert (= (M_m #x0000 #x0000) #x00000000))

(assert (= (M_r #xFFFFF298 #x00000000 #x000004C8) #xF29804C8))

(assert (= result #xF29804C8))

(assert (= result (HP_SP a_h a_l b_h b_l)))

(echo "[INFO] Trying HP_SP on a valid example, should be SAT and it is: ")
(check-sat)
(pop) ; Reset up to Push 7

; Verify there is no inconsistent case (shall be unsat)
(push) ; Push 8
(assert (not (= result (HP_SP a_h a_l b_h b_l))))
(echo "[INFO] Proving that the configuration is equivalent to HP_SP, should be UNSAT and it is: ")
(check-sat)
(pop) ; Reset up to Push 8

; Empty the assert queue
(pop) ; Reset up to Push 6

; ------------------------------------
; Testing original MBWMAC with a fault 
; ------------------------------------
; No test with faulty multiplier


; -------------------
; END OF VERIFICATION  
; -------------------
(echo "[INFO] End of the verification.")
